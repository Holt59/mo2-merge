cmake_minimum_required(VERSION 3.16)

set(PLUGIN_NAME "plugin_python")

add_library(proxy SHARED)
mo2_configure_plugin(proxy
    WARNINGS 4
    EXTERNAL_WARNINGS 4
    TRANSLATIONS OFF
    EXTRA_TRANSLATIONS
    ${CMAKE_CURRENT_SOURCE_DIR}/../runner
    ${CMAKE_CURRENT_SOURCE_DIR}/../mobase
    ${CMAKE_CURRENT_SOURCE_DIR}/../pybind11-qt)
target_link_libraries(proxy PRIVATE runner)
set_target_properties(proxy PROPERTIES OUTPUT_NAME ${PLUGIN_NAME})
mo2_install_target(proxy FOLDER)

set(PLUGIN_PYTHON_DIR bin/plugins/${PLUGIN_NAME})

# install runner
target_link_options(proxy PRIVATE "/DELAYLOAD:runner.dll")
mo2_install_target(runner INSTALLDIR ${PLUGIN_PYTHON_DIR}/dlls)

# translations (custom location)
mo2_add_translations(proxy
    TS_FILE ${CMAKE_CURRENT_SOURCE_DIR}/../${PLUGIN_NAME}_en.ts
    SOURCES
    ${CMAKE_CURRENT_SOURCE_DIR}
    ${CMAKE_CURRENT_SOURCE_DIR}/../runner
    ${CMAKE_CURRENT_SOURCE_DIR}/../mobase
    ${CMAKE_CURRENT_SOURCE_DIR}/../pybind11-qt)

# install DLLs files needed
set(DLL_DIRS ${PLUGIN_PYTHON_DIR}/dlls)
file(GLOB dlls_to_install
	# ${PYTHON_BUILD_PATH}/libffi*.dll
	${Python_HOME}/python${Python_VERSION_MAJOR}*.dll)
install(FILES ${dlls_to_install} DESTINATION ${DLL_DIRS})

# install Python .pyd files
set(PYLIB_DIR ${PLUGIN_PYTHON_DIR}/libs)
file(GLOB libs_to_install ${Python_DLL_DIR}/*.pyd)
install(FILES ${libs_to_install} DESTINATION ${PYLIB_DIR})

# generate + install standard library
set(pythoncore_zip "${CMAKE_CURRENT_BINARY_DIR}/pythoncore.zip")
add_custom_command(
    TARGET proxy POST_BUILD
    COMMAND ${Python_EXECUTABLE}
        "${CMAKE_CURRENT_SOURCE_DIR}\\build_pythoncore.py"
        ${pythoncore_zip}
    )
install(FILES ${pythoncore_zip} DESTINATION ${PYLIB_DIR})

# install mobase
install(TARGETS mobase DESTINATION ${PYLIB_DIR})

# install PyQt6
# set(PYQT_LIB_DIR ${CMAKE_BINARY_DIR}/pylibs/PyQt${Qt_VERSION_MAJOR})
# set(PYQT_TARGET_DIR ${PYLIB_DIR}/PyQt${Qt_VERSION_MAJOR})
# file(GLOB pyqt_files ${PYQT_LIB_DIR}/*.py ${PYQT_LIB_DIR}/*.pyd ${PYQT_LIB_DIR}/*.pyi)
install(
    DIRECTORY ${CMAKE_BINARY_DIR}/pylibs/PyQt${Qt_VERSION_MAJOR}
    DESTINATION ${PYLIB_DIR}
    PATTERN "*.pyd"
    PATTERN "*.pyi"
    PATTERN "__pycache__" EXCLUDE
    PATTERN "bindings" EXCLUDE
    PATTERN "lupdate" EXCLUDE
    PATTERN "Qt6" EXCLUDE
    PATTERN "uic" EXCLUDE
)
