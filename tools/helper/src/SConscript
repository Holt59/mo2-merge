Import('qt_env')

env = qt_env.Clone()

modules = [
    'Core',
]

env.EnableQt4Modules(['Qt' + n for n in modules],
                     debug = env['CONFIG'] == 'debug')

env.AppendUnique(CPPDEFINES = [
    '_UNICODE',
    '_CRT_SECURE_NO_WARNINGS'
])

env.AppendUnique(LIBS = 'advapi32')

# If I use ' '.join, somehow or other extra "s get added to the command line
# and then it goes horribly wrong
env.AppendUnique(LINKFLAGS = [
    '/SUBSYSTEM:CONSOLE',
    '/MANIFESTUAC:"' + ''.join(("level='requireAdministrator'",
                                "uiAccess='false'")) + '"',
    '${EXE_MANIFEST_DEPENDENCY}'
])

prog = env.Program('helper', env.Glob('*.cpp'))

env.Install(env['INSTALL_PATH'],
            filter(lambda x: str(x).endswith('.exe') or str(x).endswith('.pdb'),
                   prog))
